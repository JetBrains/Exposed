buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url "http://dl.bintray.com/kotlin/kotlin"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
    id 'net.researchgate.release' version '2.3.4'
}

apply plugin: 'kotlin'
apply plugin: "maven-publish"

defaultTasks 'jar'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

repositories {
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url "http://dl.bintray.com/kotlin/kotlin"
    }
}

dependencies {
    testCompile "mysql:mysql-connector-mxj:5.0.12"
    testCompile 'org.postgresql:postgresql:9.4.1208.jre7'
    testCompile 'ru.yandex.qatools.embed:postgresql-embedded:1.13'
    testCompile 'org.xerial:sqlite-jdbc:3.14.2'
}

test {
    jvmArgs "-XX:MaxPermSize=256m"
}

publishing {
    publications {
        exposed(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifactId = 'exposed'
        }
        spring_transaction(MavenPublication) {
            from findProject(":spring-transaction").components.java
            artifact sourcesJar
            artifactId = 'spring-transaction'
        }
    }
}


bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['exposed', 'spring_transaction']
    pkg {
        userOrg = 'kotlin'
        repo = 'exposed'
        name = 'exposed'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/JetBrains/Exposed.git'
    }
}

afterReleaseBuild.dependsOn bintrayUpload

task wrapper(type: Wrapper) {
    gradleVersion = "2.12"
}


allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    group = 'org.jetbrains.exposed'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        compile 'joda-time:joda-time:2.5'
        compile 'org.slf4j:slf4j-api:1.7.12'
        compileOnly 'com.h2database:h2:1.4.186'

        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        testCompile 'org.slf4j:slf4j-log4j12:1.7.12'
        testCompile 'log4j:log4j:1.2.17'
        testCompile 'junit:junit:4.11'
        testCompile 'com.h2database:h2:1.4.186'

    }
}


project(":spring-transaction") {
    apply plugin: 'kotlin'


    dependencies {
        compile 'org.springframework:spring-jdbc:4.2.5.RELEASE'
        compile 'org.springframework:spring-context:4.2.5.RELEASE'
        compile rootProject
        testCompile 'org.springframework:spring-test:4.2.5.RELEASE'
    }
}
